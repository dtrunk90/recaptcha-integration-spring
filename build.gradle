apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.dtrunk90'
archivesBaseName = 'recaptcha-spring-integration'
version = '2.0.1'

repositories {
	jcenter()
}

dependencies {
	compile 'com.fasterxml.jackson.core:jackson-databind:2.9.6'
	compile 'javax.servlet:javax.servlet-api:4.0.1'
	compile 'javax.validation:validation-api:2.0.1.Final'
	compile 'org.slf4j:slf4j-api:1.7.25'
	compile 'org.springframework:spring-webmvc:5.0.9.RELEASE'
	compile 'org.springframework.boot:spring-boot-autoconfigure:2.0.5.RELEASE'
	compile 'org.springframework.security:spring-security-core:5.0.8.RELEASE'

	testCompile 'junit:junit:4.12'
	testCompile 'org.glassfish:javax.el:3.0.0'
	testCompile 'org.hibernate:hibernate-validator:6.0.13.Final'
	testCompile 'org.slf4j:slf4j-simple:1.7.25'
	testCompile 'org.springframework:spring-test:5.0.9.RELEASE'
}

task javadocJar(type: Jar) {
	classifier = 'javadoc'
	from javadoc
}

task sourcesJar(type: Jar) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

artifacts {
	archives javadocJar, sourcesJar
}

signing {
	sign configurations.archives
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}

			pom.project {
				name 'reCAPTCHA Spring Integration'
				packaging 'jar'
				description 'reCAPTCHA Integration into Spring Framework'
				url 'https://github.com/dtrunk90/recaptcha-spring-integration'

				scm {
					connection 'scm:git:git://github.com/dtrunk90/recaptcha-spring-integration.git'
					developerConnection 'scm:git:git@github.com:dtrunk90/recaptcha-spring-integration.git'
					url 'https://github.com/dtrunk90/recaptcha-spring-integration'
				}

				licenses {
					license {
						name 'The Apache License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
					}
				}

				developers {
					developer {
						id 'dtrunk90'
						name 'Danny Trunk'
						email 'dtrunk90@gmail.com'
					}
				}
			}
		}
	}
}
